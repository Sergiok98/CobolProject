      ******************************************************************
      *    I D E N T I F I C A T I O N   D I V I S I O N               *
      ******************************************************************
      * PGM QUE MEDIANTE UN CURSOR SELECCIONA LOS CLIENTES QUE TIENEN
      * SEGUROS NO MAPFRE Y LOS ESCRIBE EN SALIDA
      ******************************************************************
      *
       IDENTIFICATION DIVISION.
      *
      ******************************************************************
      *
       PROGRAM-ID. CLIVACR1.
       AUTHOR. SERGIO.
       DATE-WRITTEN. 09/10/2024.
      *
      ******************************************************************
      *    E N V I R O N M E N T       D I V I S I O N                 *
      ******************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
         SOURCE-COMPUTER. IBM-3090.
         OBJECT-COMPUTER. IBM-3090.
      *
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
      *
       FILE-CONTROL.
      *
           SELECT FSALIDA
               ASSIGN TO FSALIDA
               FILE STATUS IS FS-FSALIDA.
      *
      ******************************************************************
      *    D A T A   D I V I S I O N                                   *
      ******************************************************************
       DATA DIVISION.
      *
       FILE SECTION.
      *
      ******************************************************************
      *    F I L E   S E C T I O N                                     *
      ******************************************************************
      *
       FD FSALIDA
           RECORDING MODE IS F.
      *
       01  REG-FSALIDA             PIC X(710).
      *
      ******************************************************************
      *    W O R K I N G    S T O R A G E                              *
      ******************************************************************
       WORKING-STORAGE SECTION.
      *
       01 CA-CONSTANTES-ALF.
          05 CA-PGM                     PIC X(08) VALUE 'CLIVACR1'.
          05 CA-00                      PIC X(02) VALUE '00'.
          05 CA-10                      PIC X(02) VALUE '10'.
          05 CA-MAPFRE                  PIC X(06) VALUE 'MAPFRE'.
      *
       01 CN-CONTADORES.
          05 CN-REG-CONSULTADOS         PIC 9(03).
          05 CN-REG-ESCRITOS-FSALIDA    PIC 9(03).
      *
       01 WK-VARIAVLES.
          05 WK-SQLCODE                  PIC -999.
      *
       01 FS-FILE-STATUS.
          05 FS-FENTRADA                PIC X(02).
          05 FS-FSALIDA                 PIC X(02).
      *
       01 SW-SWITCHES.
          05 SW-FIN-CURSOR              PIC X(01).
             88 SW-SI-FIN-CURSOR                 VALUE 'S'.
             88 SW-NO-FIN-CURSOR                 VALUE 'N'.
      *
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.
      *
      * DCL DE CLIENTES-PEPITO-SEG
      *
           EXEC SQL
               INCLUDE CLIPEPTB
           END-EXEC.
      *
      * DECLARACION DEL CURSOR QUE MUESTRA LOS CLIENTES CON SEGUROS
      * NO MAPFRE, EVITANDO LA DUPLICIDAD DE REGISTROS
      *
           EXEC SQL
                DECLARE CRCLIPEPTB CURSOR FOR
                 SELECT *
                   FROM CLIENTES_PEPITO_SEG C
                  WHERE EXISTS (
                                SELECT 1
                                FROM SEGUROS_PEPITO_SEG S
                                JOIN COMPANIAS_SEGUROS CS
                                ON S.NUMERO_POLIZA = CS.NUMERO_POLIZA
                                WHERE C.DNI_CL = S.DNI_CL
                                AND CS.NOMBRE_COMPANIA <> :CA-MAPFRE
                                )
                          ORDER BY C.DNI_CL
           END-EXEC.
      *
      ******************************************************************
      *    PROCEDURE DIVISION                                          *
      ******************************************************************
       PROCEDURE DIVISION.
      *
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT
      *
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
      *
           PERFORM 3000-FIN
              THRU 3000-FIN-EXIT
      *
           .
      *
      ******************************************************************
      * 1000-INICIO
      ******************************************************************
      *
       1000-INICIO.
      *
           INITIALIZE CN-CONTADORES
                      FS-FILE-STATUS
                      DCLCLIENTES-PEPITO-SEG
      *
           SET SW-NO-FIN-CURSOR         TO TRUE
      *
           PERFORM 1100-ABRIR-FICHEROS
              THRU 1100-ABRIR-FICHEROS-EXIT
      *
           PERFORM 1200-ABRIR-CURSOR
              THRU 1200-ABRIR-CURSOR-EXIT
      *
           .
      *
       1000-INICIO-EXIT.
           EXIT.
      *
      ******************************************************************
      * 1100-ABRIR-FICHEROS
      ******************************************************************
      *
       1100-ABRIR-FICHEROS.
      *
           OPEN OUTPUT FSALIDA
      *
           IF FS-FSALIDA NOT = CA-00
              DISPLAY 'ERROR AL ABRIR EL FICHERO DE SALIDA'
              DISPLAY 'FS-FSALIDA: ' FS-FSALIDA
              DISPLAY '1100-ABRIR-FICHEROS'
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           .
      *
       1100-ABRIR-FICHEROS-EXIT.
           EXIT.
      *
      ******************************************************************
      * 1200-ABRIR-CURSOR
      ******************************************************************
      *
       1200-ABRIR-CURSOR.
      *
           EXEC SQL
                OPEN CRCLIPEPTB
           END-EXEC.
      *
           IF SQLCODE NOT = 0
              DISPLAY 'ERROR ABRIR CURSOR'
              MOVE SQLCODE TO WK-SQLCODE
              DISPLAY 'SQLCODE: ' WK-SQLCODE
              DISPLAY '1200-ABRIR-CURSOR'

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT

           END-IF
      *
           .
      *
       1200-ABRIR-CURSOR-EXIT.
           EXIT.
      *
      ******************************************************************
      * 2000-PROCESO
      ******************************************************************
      *
       2000-PROCESO.
      *
           PERFORM 2100-LEER-CURSOR
              THRU 2100-LEER-CURSOR-EXIT
             UNTIL SW-SI-FIN-CURSOR
      *
           .
      *
       2000-PROCESO-EXIT.
           EXIT.
      *
      ******************************************************************
      * 2100-LEER-CURSOR
      ******************************************************************
      *
       2100-LEER-CURSOR.
      *
           EXEC SQL
               FETCH CRCLIPEPTB
                INTO
                     :TB-DNI-CL
                    ,:TB-NOMBRE-CL
                    ,:TB-APELLIDO-1
                    ,:TB-APELLIDO-2
                    ,:TB-CLASE-VIA
                    ,:TB-NOMBRE-VIA
                    ,:TB-NUMERO-VIA
                    ,:TB-COD-POSTAL
                    ,:TB-CIUDAD
                    ,:TB-TELEFONO
                    ,:TB-OBSERVACIONES
           END-EXEC

      *
           EVALUATE SQLCODE
               WHEN 0
                    ADD 1            TO CN-REG-CONSULTADOS
                    PERFORM 2500-ESCRIBIR-SALIDA
                       THRU 2500-ESCRIBIR-SALIDA-EXIT
               WHEN 100
                    SET SW-SI-FIN-CURSOR TO TRUE
      *
               WHEN OTHER
                    DISPLAY 'ERROR DB2'
                    DISPLAY 'FETCH CRCLIPEPTB'
                    DISPLAY 'TABLA: CLIENTES-PEPITO-SEG'
                    MOVE SQLCODE TO WK-SQLCODE
                    DISPLAY 'SQLCODE: ' WK-SQLCODE

                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
      *
       2100-LEER-CURSOR-EXIT.
           EXIT.
      *
      ******************************************************************
      * 2500-ESCRIBIR-SALIDA
      ******************************************************************
      *
       2500-ESCRIBIR-SALIDA.
      *
           WRITE REG-FSALIDA FROM DCLCLIENTES-PEPITO-SEG

           IF FS-FSALIDA NOT = CA-00
              DISPLAY 'ERROR AL ESCRIBIR EN EL FICHERO DE SALIDA'
              DISPLAY 'FS-FSALIDA: ' FS-FSALIDA
              DISPLAY '2100-ESCRIBIR-SALIDA'

           PERFORM 3000-FIN
              THRU 3000-FIN-EXIT

           ELSE
              INITIALIZE DCLCLIENTES-PEPITO-SEG
              ADD 1 TO CN-REG-ESCRITOS-FSALIDA
           END-IF
      *
           .
      *
       2500-ESCRIBIR-SALIDA-EXIT.
           EXIT.
      *
      ******************************************************************
      * 3000-FIN
      ******************************************************************
      *
       3000-FIN.
      *
           PERFORM 3100-CERRAR-FICHEROS
              THRU 3100-CERRAR-FICHEROS-EXIT
      *
           PERFORM 3150-CERRAR-CURSOR
              THRU 3150-CERRAR-CURSOR-EXIT
      *
           PERFORM 3200-ESTADISTICAS
              THRU 3200-ESTADISTICAS-EXIT
      *
           STOP RUN
      *
           .
      *
       3000-FIN-EXIT.
           EXIT.
      *
      ******************************************************************
      * 3100-CERRAR-FICHEROS
      ******************************************************************
      *
       3100-CERRAR-FICHEROS.
      *
           CLOSE FSALIDA
      *
           IF FS-FSALIDA NOT = CA-00
              DISPLAY 'ERROR AL CERRAR FICHERO DE SALIDA'
              DISPLAY 'FS-FSALIDA: ' FS-FSALIDA
              DISPLAY '3100-CERRAR-FICHEROS'
           END-IF
      *
           .
      *
       3100-CERRAR-FICHEROS-EXIT.
           EXIT.
      *
      ******************************************************************
      * 3150-CERRAR-CURSOR
      ******************************************************************
      *
       3150-CERRAR-CURSOR.
      *
           EXEC SQL
               CLOSE CRCLIPEPTB
           END-EXEC.
      *
           IF SQLCODE NOT = 0
              DISPLAY 'ERROR AL CERRAR EL SURSOR'
              DISPLAY 'CURSOR: CRCLIPEPTB'
              MOVE SQLCODE TO WK-SQLCODE
              DISPLAY 'SQLCODE: ' WK-SQLCODE
           END-IF
      *
           .
      *
       3150-CERRAR-CURSOR-EXIT.
           EXIT.
      *
      ******************************************************************
      * 3200-ESTADISTICAS
      ******************************************************************
      *
       3200-ESTADISTICAS.
      *
           DISPLAY '************************************************'
           DISPLAY '****    ESTADISTICAS DEL PGM CLIVACR1    ********'
           DISPLAY '************************************************'
           DISPLAY '*REG. CONSULTADOS: ' CN-REG-CONSULTADOS
           DISPLAY '*REG. ESCRITOS EN SALIDA: ' CN-REG-ESCRITOS-FSALIDA
      *
           .
      *
       3200-ESTADISTICAS-EXIT.
           EXIT.
      *
