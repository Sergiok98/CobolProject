      ******************************************************************
      ** R U T C O N S 2.-RUTINA QUE RECIBE EL VALOR DEL ASEGURADO Y  **
      **                  TIPO DE SEGURO Y CALCULA LA PRIMA ANUAL     **
      ******************************************************************
      *
      ******************************************************************
      ** IDENTIFICATION DIVISION                                      **
      ******************************************************************
      *
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID. RUTEJ1.
       AUTHOR. SERGIO.
       DATE-WRITTEN. 18/09/2024.
      *
      ******************************************************************
      ** ENVIRONMENT DIVISION                                         **
      ******************************************************************
      *
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *
      ******************************************************************
      ** DATA DIVISION                                                **
      ******************************************************************
      *
       DATA DIVISION.
      *
       FILE SECTION.
      *
       WORKING-STORAGE SECTION.
      *
       01 CA-CONSTANTES.
          05 CA-RUTINA                    PIC X(7) VALUE 'RUTEJ1'.
          05 CA-00                        PIC X(2) VALUE '00'.
          05 CA-99                        PIC X(2) VALUE '99'.
          05 CA-100                       PIC X(3) VALUE '100'.
          05 CA-88                        PIC X(2) VALUE '88'.
      *
       01 CN-CONSTANTES.
          05 CN-0                         PIC 9 VALUE 0.
          05 CN-100                       PIC 9(3) VALUE 100.
      *
       01 WK-VARIABLES.
          05 WK-PREF                      PIC X(2).
      *
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.
      *
           EXEC SQL
               INCLUDE TBPROVIN
           END-EXEC.
      *
       LINKAGE SECTION.
      *
      *COPY DE LA RUTINA RUTOPERA
      *
       COPY CPYREJR1.
      *
      ******************************************************************
      ** PROCEDURE DIVISION                                           **
      ******************************************************************
      *
       PROCEDURE DIVISION USING CPY-EJRUT01.
      *
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT
      *
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
      *
           PERFORM 3000-FIN
              THRU 3000-FIN-EXIT
      *
           .
      *
      ******************************************************************
      ** 1000-INICIO                                                  **
      ** INICIALIZAR VARIABLES                                        **
      ******************************************************************
      *
       1000-INICIO.
      *
           INITIALIZE SALIDA-RUT
                      ERRORES-RUTINA
                      WK-VARIABLES
                      DCLPROVINCIAS
      *
           MOVE CA-00                    TO COD-RETORNO
      *
           IF VALOR-DIR-RUT = SPACES OR LOW-VALUES
              MOVE CA-99                 TO COD-RETORNO
              MOVE CA-00                 TO COD-SUBRETORNO
              MOVE '1000-INICIO'         TO PARRAFO-RUT
              MOVE CA-RUTINA             TO NOMBRE-RUT
              MOVE 'EL TIPO DE VALOR SIN INFORMAR'
                                         TO DESCRIPCION-RUT
              MOVE 'VALOR-DIR-RUT'
                                         TO ELEMENTO-RUT
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
           .
      *
       1000-INICIO-EXIT.
           EXIT.
      *
      ******************************************************************
      ** 2000-PROCESO                                                 **
      ** BUSCAR EN LA TABLA PROVINCIAS EL PREFIJO Y GUARDAR EN LA     **
      ** DCLGEN DE LA TABLA LOS CAMPOS PREFIJO Y PROVINCIAS           **
      ******************************************************************
      *
       2000-PROCESO.
      *
           MOVE VALOR-DIR-RUT(1:2) TO TB-PREFIJO
      *
           EXEC SQL
              SELECT
                  PROVINCIA
              INTO
                  :TB-PROVINCIA
              FROM PROVINCIAS
              WHERE PREFIJO = :TB-PREFIJO
           END-EXEC
      *
           EVALUATE SQLCODE
               WHEN CN-0
                    MOVE TB-PROVINCIA     TO PROVINCIA-RUT
      *
               WHEN CN-100
                    MOVE CA-100           TO COD-RETORNO
                    MOVE CA-88            TO COD-SUBRETORNO
                    MOVE CA-RUTINA        TO NOMBRE-RUT
                    MOVE '2000-PROCESO'   TO PARRAFO-RUT
                    MOVE 'PREFIJO NO ENCONTRADO'
                         TO DESCRIPCION-RUT
                    MOVE SQLCODE          TO SQLCODE-RUT
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT
      *
               WHEN OTHER
                    MOVE CA-99            TO COD-RETORNO
                    MOVE CA-99            TO COD-SUBRETORNO
                    MOVE CA-RUTINA        TO NOMBRE-RUT
                    MOVE '2000-PROCESO'   TO PARRAFO-RUT
                    MOVE 'SQL ERROR'
                         TO DESCRIPCION-RUT
                    MOVE SQLCODE          TO SQLCODE-RUT
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           .
      *
       2000-PROCESO-EXIT.
           EXIT.
      *
      *
      ******************************************************************
      ** 3000-FIN                                                     **
      ** FINALIZAMOS EL PGM.                                          **
      ******************************************************************
      *
       3000-FIN.
      *
           GOBACK
      *
           .
      *
       3000-FIN-EXIT.
           EXIT.
      *
