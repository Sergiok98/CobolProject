      ******************************************************************
      **      I D E N T I F I C A T I O N   D I V I S I O N           **
      ******************************************************************
       IDENTIFICATION DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID. PGMREPOS.
       AUTHOR. CRISTIAN TORRES.
      ******************************************************************
      **      E N V I R O N M E N T   D I V I S I O N                 **
      ******************************************************************
       ENVIRONMENT DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *--------------------- FICHEROS DE ENTRADA ----------------------*
           SELECT FENTRADA
           ASSIGN TO FENTRADA
           FILE STATUS IS FS-FENTRADA.
      *--------------------- FICHEROS DE SALIDA  ----------------------*
           SELECT FINCIDEN
           ASSIGN TO FINCIDEN
           FILE STATUS IS FS-FINCIDEN.
      ******************************************************************
      **      D A T A   D I V I S I O N                               **
      ******************************************************************
       DATA DIVISION.
      *----------------------------------------------------------------*
       FILE SECTION.
       FD FENTRADA
            BLOCK CONTAINS 0 RECORDS
            LABEL RECORD STANDARD
            RECORDING MODE IS F.
       01 REG-FENTRADA            PIC X(149).
      *----------------------------------------------------------------*
       FD FINCIDEN
            BLOCK CONTAINS 0 RECORDS
            LABEL RECORD STANDARD
            RECORDING MODE IS F.
       01 REG-FINCIDEN            PIC X(149).
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *--------------------- CONSTANTES ALFANUMERICAS -----------------*
       01 CA-CONSTANTES-ALFANUMERICAS.
          05 CA-00                PIC X(02)         VALUE '00'.
          05 CA-10                PIC X(02)         VALUE '10'.
          05 CA-PGMREPOS          PIC X(08)         VALUE 'PGMREPOS'.
          05 CA-KO                PIC X(02)         VALUE 'KO'.
          05 CA-OK                PIC X(02)         VALUE 'OK'.
      *--------------------- CONSTANTES NUMERICAS ---------------------*
       01 CN-CONSTANTES-NUMERICAS.
          05 CN-100               PIC 9(03)         VALUE 100.
      *--------------------- FILE STATUS ------------------------------*
       01 FS-FILE-STATUS.
          05 FS-FENTRADA          PIC X(02).
          05 FS-FINCIDEN          PIC X(02).
      *--------------------- SWITCHES ---------------------------------*
       01 SW-FIN-FENTRADA         PIC X(01).
          88 SI-FIN-FENTRADA      VALUE 'S'.
          88 NO-FIN-FENTRADA      VALUE 'N'.
      *--------------------- CONTADORES -------------------------------*
       01 CNT-CONTADORES.
          05 CNT-REG-FENTRADA     PIC 9(09).
          05 CNT-REG-ESCRITO      PIC 9(09).
          05 CNT-REG-INSERT       PIC 9(09).
          05 CNT-REG-UPDATE       PIC 9(09).
      *--------------------- VARIABLES --------------------------------*
       01 WK-VARIABLES.
          05 WK-SQLCODE           PIC 09(10).
          05 WK-CLAVE-ENT.
             10 SELECCION-CLAVE   PIC X(30).
      *--------------------- DECLARACIONES SQL ------------------------*
       EXEC SQL INCLUDE SQLCA END-EXEC.
       EXEC SQL INCLUDE TBMUNDI  END-EXEC.
       EXEC SQL INCLUDE TBDAREPO END-EXEC.
      ******************************************************************
      **      P R O C E D U R E   D I V I S I O N                     **
      ******************************************************************
       PROCEDURE DIVISION.
      *----------------------------------------------------------------*
           PERFORM INICIO
              THRU INICIO-EXIT
      *
           PERFORM PROCESO
              THRU PROCESO-EXIT
             UNTIL SI-FIN-FENTRADA
      *
           PERFORM FIN
              THRU FIN-EXIT
           .
      *--------------------- INICIO -----------------------------------*
       INICIO.
           INITIALIZE CNT-CONTADORES
                      FS-FILE-STATUS
                      WK-VARIABLES
                      DCLDAREPOS
                      DCLMUNDIAL
      *
           SET NO-FIN-FENTRADA TO TRUE
      *
           PERFORM ABRIR-FICHEROS
              THRU ABRIR-FICHEROS-EXIT
      *
           MOVE CA-PGMREPOS  TO TB-NOMBRE-PGM
      *
           PERFORM CONSULTAR-DAREPOS
              THRU CONSULTAR-DAREPOS-EXIT
           .
      *
       INICIO-EXIT.
           EXIT.
      *--------------------- ABRIR-FICHEROS ---------------------------*
       ABRIR-FICHEROS.
           OPEN INPUT FENTRADA
      *
           OPEN OUTPUT FINCIDEN
      *
           IF FS-FENTRADA NOT = CA-00
              DISPLAY 'ERROR EN LA APERTURA DEL FICHERO DE FENTRADA'
              DISPLAY 'NOMBRE DEL FICHERO: FENTRADA'
              DISPLAY 'FILE STATUS: ' FS-FENTRADA
              PERFORM FIN
                 THRU FIN-EXIT
           END-IF
      *
           IF FS-FINCIDEN NOT = CA-00
              DISPLAY 'ERROR EN LA APERTURA DEL FICHERO DE FINCIDEN '
              DISPLAY 'NOMBRE DEL FICHERO: FINCIDEN '
              DISPLAY 'FILE STATUS: ' FS-FINCIDEN
              PERFORM FIN
                 THRU FIN-EXIT
           END-IF
      *
           .
      *
       ABRIR-FICHEROS-EXIT.
           EXIT.
      *--------------------- CONSULTAR DAREPOS -------------------------*
       CONSULTAR-DAREPOS.
      *
           DISPLAY 'TB-NOMBRE-PGM: ' TB-NOMBRE-PGM
      *
           EXEC SQL
              SELECT ESTADO
                    ,VALOR_CLAVE
                 INTO
                    :TB-ESTADO
                   ,:TB-VALOR-CLAVE-TEXT
                 FROM DAREPOS
              WHERE NOMBRE_PGM = :TB-NOMBRE-PGM
           END-EXEC.
      *
           EVALUATE SQLCODE
      *
              WHEN 0
      *
                 EVALUATE TB-ESTADO
                    WHEN CA-KO
                       DISPLAY 'VOY A LEER ENTRADA'
                       PERFORM LEER-ENTRADA
                          THRU LEER-ENTRADA-EXIT
                         UNTIL WK-CLAVE-ENT > TB-VALOR-CLAVE-TEXT
      *
                    WHEN CA-OK
                       PERFORM LEER-ENTRADA
                          THRU LEER-ENTRADA-EXIT
      *
                    WHEN OTHER
                       DISPLAY 'ERROR: CAMPO TB-ESTADO NO VALIDO'
                       PERFORM FIN
                          THRU FIN-EXIT
                 END-EVALUATE
      *
              WHEN 100
      *
                 PERFORM INSERTAR-DAREPOS
                    THRU INSERTAR-DAREPOS-EXIT
                 PERFORM LEER-ENTRADA
                    THRU LEER-ENTRADA-EXIT
      *
              WHEN OTHER
      *
                 DISPLAY 'ERROR SELECT DE DAREPOS'
                 DISPLAY 'SQLCODE: ' SQLCODE
                 MOVE SQLCODE TO WK-SQLCODE
                 DISPLAY 'SQLCODE BIEN: 'WK-SQLCODE
                 DISPLAY 'PARRAFO: CONSULTAR-DAREPOS'
                 DISPLAY 'TABLA: DAREPOS'
                 PERFORM FIN
                    THRU FIN-EXIT
           END-EVALUATE
           .
       CONSULTAR-DAREPOS-EXIT.
           EXIT
           .
      *--------------------- INSERTAR DAREPOS --------------------------*
       INSERTAR-DAREPOS.
           MOVE CA-KO    TO TB-ESTADO
           INITIALIZE TB-VALOR-CLAVE-TEXT
      *
           EXEC SQL
              INSERT INTO DAREPOS VALUES (:TB-NOMBRE-PGM,
                                          :TB-ESTADO,
                                          :TB-VALOR-CLAVE-TEXT)
           END-EXEC.
      *
           EVALUATE SQLCODE
              WHEN 0
                 CONTINUE
              WHEN -803
                 DISPLAY 'ERROR AL INSERTAR EN DAREPOS POR REG DUPLI'
                 DISPLAY 'RETORNO: 99'
                 DISPLAY 'SQLCODE: ' SQLCODE
                 DISPLAY 'PARRAFO: INSERTAR-DAREPOS'
                 DISPLAY 'TABLA  : DAREPOS'
                 DISPLAY 'ERROR: NO ES PUSIBLA'
                 PERFORM FIN
                    THRU FIN-EXIT
              WHEN OTHER
                 DISPLAY 'ERROR AL INSERTAR EN DAREPOS0'
                 DISPLAY 'RETORNO: 99'
                 DISPLAY 'SQLCODE: ' SQLCODE
                 MOVE SQLCODE TO WK-SQLCODE
                 DISPLAY 'SQLCODE BIEN: 'WK-SQLCODE
                 DISPLAY 'PARRAFO: INSERTAR-DAREPOS'
                 DISPLAY 'TABLA  : DAREPOS'
                 PERFORM FIN
                    THRU FIN-EXIT
           END-EVALUATE
           .
       INSERTAR-DAREPOS-EXIT.
           EXIT
           .
      *--------------------- LEER ENTRADA ------------------------------*
       LEER-ENTRADA.
           READ FENTRADA INTO DCLMUNDIAL
      *
           EVALUATE FS-FENTRADA
      *
             WHEN CA-00
                ADD 1 TO CNT-REG-FENTRADA
                MOVE TB-SELECCION   TO SELECCION-CLAVE
      *
             WHEN CA-10
                DISPLAY 'HE LEIDO Y SE HA ACABADO'
                SET SI-FIN-FENTRADA TO TRUE
                PERFORM DAREPOS-OK
                   THRU DAREPOS-OK-EXIT
      *
             WHEN OTHER
                DISPLAY 'HE LEIDO Y ALGO HA IDO MUY MUY MAL'
                DISPLAY 'ERROR AL LEER FICHERO DE FENTRADA'
                DISPLAY 'NOMBRE DEL FICHERO: FENTRADA'
                DISPLAY 'FILE STATUS: ' FS-FENTRADA
                PERFORM FIN
                   THRU FIN-EXIT
      *
           END-EVALUATE
           .
      *
       LEER-ENTRADA-EXIT.
           EXIT
           .
      *--------------------- PROCESO ----------------------------------*
       PROCESO.
           PERFORM INSERT-MUNDIAL
              THRU INSERT-MUNDIAL-EXIT
      *
           PERFORM UPDATE-DAREPOS
              THRU UPDATE-DAREPOS-EXIT
      *
           PERFORM LEER-ENTRADA
              THRU LEER-ENTRADA-EXIT
           .
       PROCESO-EXIT.
           EXIT
           .
      *--------------------- INSERT MAPFRE --------------------------*
       INSERT-MUNDIAL.

           EXEC SQL
              INSERT INTO MUNDIAL
                VALUES (
                    :TB-SELECCION,
                    :TB-ENTRENADOR,
                    :TB-NUM-JUGADORES-CONV,
                    :TB-FECHA-PRIMER-PARTIDO,
                    :TB-GRUPO,
                    :TB-FASE-ELIMINACION,
                    :TB-TIMESTAMP-MODIF,
                    :TB-USUARIO-MODIF
                 )
           END-EXEC.

           EVALUATE SQLCODE
               WHEN 0
                  ADD 1 TO CNT-REG-INSERT
               WHEN -803
                  PERFORM ESCRIBIR-INCIDENCIA
                     THRU ESCRIBIR-INCIDENCIA-EXIT
               WHEN OTHER
                  DISPLAY 'ERROR AL INSERTAR EN LA TABLA MUNDIAL'
                  DISPLAY 'RETORNO: 99'
                  DISPLAY 'SQLCODE: ' SQLCODE
                  DISPLAY 'PARRAFO: INSERT-MUNDIAL'
                  DISPLAY 'TABLA  : MUNDIAL      '
                  PERFORM ESCRIBIR-INCIDENCIA
                     THRU ESCRIBIR-INCIDENCIA-EXIT
                  PERFORM FIN
                     THRU FIN-EXIT
           END-EVALUATE
           .
       INSERT-MUNDIAL-EXIT.
           EXIT
           .
      *--------------------- UPDATE DAREPOS ---------------------------*
       UPDATE-DAREPOS.
           MOVE WK-CLAVE-ENT TO TB-VALOR-CLAVE-TEXT
           MOVE CA-PGMREPOS  TO TB-NOMBRE-PGM
           MOVE 'KO'         TO TB-ESTADO
      *
           EXEC SQL
              UPDATE DAREPOS
                 SET VALOR_CLAVE = :TB-VALOR-CLAVE-TEXT
                    ,ESTADO      = :TB-ESTADO
               WHERE NOMBRE_PGM  = :TB-NOMBRE-PGM
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0
                 CONTINUE
      *          IF CNT-REG-FENTRADA > 5
      *             PERFORM 3000-FIN
      *                THRU 3000-FIN-EXIT
      *          END-IF
              WHEN OTHER
                 DISPLAY 'ERROR EN EL UPDATE DE DAREPOS'
                 DISPLAY 'PARRAFO: UPDATE-DAREPOS'
                 DISPLAY 'TABLA: DAREPOS'
                 DISPLAY 'SQLCODE: ' SQLCODE
                 PERFORM FIN
                    THRU FIN-EXIT
           END-EVALUATE
      *
           EXEC SQL
              COMMIT
           END-EXEC
           .
       UPDATE-DAREPOS-EXIT.
           EXIT
           .
      *--------------------- ESCRIBIR INCIDENCIA -----------------------*
       ESCRIBIR-INCIDENCIA.
      *
           WRITE REG-FINCIDEN FROM DCLMUNDIAL
      *
           EVALUATE FS-FINCIDEN
      *
             WHEN CA-00
                ADD 1 TO CNT-REG-ESCRITO
      *
             WHEN OTHER
                DISPLAY 'HE ESCRITO UN REGISTRO EN INCIDENCIA'
                DISPLAY 'ERROR AL ESCRIBIR FICHERO DE INCIDENCIAS'
                DISPLAY 'NOMBRE DEL FICHERO: FINCIDEN '
                DISPLAY 'FILE STATUS: ' FS-FINCIDEN
                PERFORM FIN
                   THRU FIN-EXIT
      *
           END-EVALUATE
           .
      *
       ESCRIBIR-INCIDENCIA-EXIT.
           EXIT
           .
      *--------------------- FIN --------------------------------------*
       FIN.
           PERFORM CERRAR-FICHEROS
              THRU CERRAR-FICHEROS-EXIT
           PERFORM MOSTRAR-ESTADISTICAS
              THRU MOSTRAR-ESTADISTICAS-EXIT
           STOP RUN
           .
      *
       FIN-EXIT.
           EXIT
           .
      *--------------------- DAREPOS OK -------------------------------*
       DAREPOS-OK.
           MOVE CA-OK TO TB-ESTADO
           INITIALIZE TB-VALOR-CLAVE-TEXT
      *
           EXEC SQL
              UPDATE DAREPOS
                 SET ESTADO      = :TB-ESTADO
                    ,VALOR_CLAVE = :TB-VALOR-CLAVE-TEXT
               WHERE NOMBRE_PGM  = :TB-NOMBRE-PGM
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0
                 ADD 1 TO CNT-REG-UPDATE
              WHEN OTHER
                 DISPLAY 'ERROR AL PONER OK EN DAREPOS'
                 DISPLAY 'PARRAFO: DAREPOS-OK'
                 DISPLAY 'TABLA: DAREPOS'
                 DISPLAY 'SQLCODE: ' SQLCODE
                 PERFORM FIN
                    THRU FIN-EXIT
           END-EVALUATE

           .
      *
       DAREPOS-OK-EXIT.
           EXIT
           .
      *--------------------- CERRAR-FICHEROS --------------------------*
       CERRAR-FICHEROS.
           CLOSE FENTRADA
                 FINCIDEN
      *
           IF FS-FENTRADA NOT = CA-00
              DISPLAY 'ERROR AL CERRAR EL FICHERO DE FENTRADA'
              DISPLAY 'NOMBRE DEL FICHERO: FENTRADA'
              DISPLAY 'FILE STATUS: ' FS-FENTRADA
           END-IF
      *
           IF FS-FINCIDEN NOT = CA-00
              DISPLAY 'ERROR AL CERRAR EL FICHERO DE FINCIDEN '
              DISPLAY 'NOMBRE DEL FICHERO: FINCIDEN '
              DISPLAY 'FILE STATUS: ' FS-FINCIDEN
           END-IF
           .
      *
       CERRAR-FICHEROS-EXIT.
           EXIT
           .
      *--------------------- MOSTRAR-ESTADISTICAS ---------------------*
       MOSTRAR-ESTADISTICAS.
           DISPLAY '**********************************************'
           DISPLAY '* E S T A D I S T I C A S                    *'
           DISPLAY '*--------------------------------------------*'
           DISPLAY '* REGISTROS LEIDOS   DE FENTRADA : '
                                              CNT-REG-FENTRADA ' *'
           DISPLAY '* REGISTROS INCIDENCIA           : '
                                              CNT-REG-ESCRITO  ' *'
           DISPLAY '* REGISTROS INSERTADOS EN CUENTAS: '
                                              CNT-REG-INSERT   ' *'
           DISPLAY '* REGISTROS UPDATEADOS EN DAREPOS: '
                                              CNT-REG-UPDATE   ' *'
           DISPLAY '**********************************************'
           .
      *
       MOSTRAR-ESTADISTICAS-EXIT.
           EXIT
           .
      *
